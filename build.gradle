plugins {
	id 'application'
	id 'checkstyle'
	id 'jacoco'
	id 'org.springframework.boot' version '3.0.6'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'org.liquibase.gradle' version '2.2.0'
	id 'org.springdoc.openapi-gradle-plugin' version '1.6.0'
	id 'com.github.johnrengelman.processes' version '0.5.0'
	id 'com.adarshr.test-logger' version '3.2.0'
	id 'com.github.ben-manes.versions' version '0.47.0'
	id 'org.siouan.frontend-jdk11' version '6.0.0'
}

group = 'hexlet.code'
version = '0.0.1-SNAPSHOT'

application {
	mainClass = 'hexlet.code.AppApplication'
}

wrapper {
	gradleVersion = '8.2'
	distributionType = Wrapper.DistributionType.ALL
}

compileJava {
	options.release.set(17)
	options.encoding = 'UTF-8'
}

jacocoTestReport {
	reports {
		xml.required = true
	}
}

checkstyle {
	toolVersion '10.12.1'
}

test {
	useJUnitPlatform()
}

testlogger {
	showStandardStreams true
}

repositories {
	mavenCentral()
}

dependencies {
	developmentOnly 'org.springframework.boot:spring-boot-devtools:3.0.4'

	compileOnly 'org.projectlombok:lombok:1.18.26'

	annotationProcessor(
			'org.projectlombok:lombok:1.18.26',
			'com.querydsl:querydsl-apt:5.0.0:jakarta',
			'jakarta.persistence:jakarta.persistence-api:3.1.0'
	)

	implementation(
			'org.springframework.boot:spring-boot-starter-data-jpa:3.0.4',
			'org.springframework.boot:spring-boot-starter-web:3.1.0',
			'org.springframework.boot:spring-boot-starter-validation:3.0.4',
			'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.2.0',
			'org.springframework.boot:spring-boot-starter-security',
			'io.jsonwebtoken:jjwt:0.9.1',
			'org.liquibase:liquibase-core:4.20.0',
			'com.querydsl:querydsl-core:5.0.0',
			'com.querydsl:querydsl-jpa:5.0.0:jakarta',
	)

	liquibaseRuntime(
			sourceSets.main.output,
			'org.liquibase:liquibase-core:4.20.0',
			'info.picocli:picocli:4.7.3',
			'org.liquibase.ext:liquibase-hibernate6:4.21.1',
			'org.springframework.boot:spring-boot-starter-data-jpa:3.0.4',
			'com.h2database:h2:2.1.214',

	)

	runtimeOnly (
			'com.h2database:h2:2.1.214',
			'org.postgresql:postgresql:42.6.0',
	)

	testImplementation (
			'org.springframework.boot:spring-boot-starter-test:3.1.0',
			'org.springframework.boot:spring-boot-starter-security',
			'org.springframework.security:spring-security-test',
	)
}

diffChangelog {
	dependsOn compileJava
}

liquibase {
	activities {
		main {
			changelogFile 'src/main/resources/db/changelog/db.changelog-master.yaml'
			url 'jdbc:h2:./taskManager'
			referenceUrl 'hibernate:spring:hexlet.code.model.' +
					'?dialect=org.hibernate.dialect.H2Dialect' +
					'&hibernate.physical_naming_strategy=org.hibernate.boot.model.naming.CamelCaseToUnderscoresNamingStrategy'
		}
	}
}

frontend {
	nodeVersion = '16.13.1'
	installScript = 'install'
	assembleScript = 'run build'
	packageJsonDirectory = file("${projectDir}/frontend")
}

def configFrontendTasks = {
	inputs.files(fileTree("$projectDir/frontend").exclude('build', 'node_modules'))
	outputs.dir("$buildDir/resources/main/static")
}

assembleFrontend {
	configure configFrontendTasks

	doLast {
		copy {
			from "$projectDir/frontend/build"
			into "$buildDir/resources/main/static"
		}
	}
}

installFrontend {
	configure configFrontendTasks
}

processResources.dependsOn assembleFrontend

tasks.named('test') {
	useJUnitPlatform()
}

task stage(dependsOn: ['clean', 'installDist'])
installDist.mustRunAfter clean
